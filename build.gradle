plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
    maven {
        name = "developmentRepository"
        url = "https://repo.pantruskawka045.me/truskawka"
        credentials(PasswordCredentials)
        authentication {
            basic(BasicAuthentication)
        }
    }
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    implementation group: 'org.jetbrains', name: 'annotations', version: '26.0.1'

    implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

}

group = 'me.pan_truskawka045'
version = '1.2-SNAPSHOT'
description = 'Spigot-Animations'

shadowJar {
    minimize()
}

java.sourceCompatibility = JavaVersion.VERSION_1_8

java {
    withJavadocJar()
}


tasks.register("sourcesJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "developmentRepository"
            url = "https://repo.pantruskawka045.me/private"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            artifact tasks.jar

            // Add sources JAR
            artifact(tasks.named("sourcesJar"))

            // Add Javadoc JAR
            artifact(tasks.named("javadocJar"))
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.test {
    useJUnitPlatform()
}
